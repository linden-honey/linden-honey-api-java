import static org.gradle.api.tasks.wrapper.Wrapper.DistributionType.ALL

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.github.lindenhoney'
version = '1.1'

sourceCompatibility = 1.9
targetCompatibility = 1.9

springBoot {
    buildInfo()
}

bootJar {
    launchScript()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
    distributionType = ALL
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "short"
    }

    failFast = true
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
/**
 *  Spring dependencies
 */
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

/**
 *  Other dependencies
 */
    compileOnly 'org.projectlombok:lombok'
    implementation "org.apache.commons:commons-lang3:${apacheCommonsVersion}"

    implementation "com.github.mongobee:mongobee:${mongobeeVersion}", {
        exclude group: 'org.mongodb', module: 'mongo-java-driver'
    }

    implementation "org.jsoup:jsoup:${jsoupVersion}"
    implementation 'io.projectreactor.addons:reactor-extra'


/**
 *  Test dependencies
 */
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test', {
        exclude group: 'junit', module: 'junit'
    }

    testImplementation 'io.projectreactor:reactor-test'
}
